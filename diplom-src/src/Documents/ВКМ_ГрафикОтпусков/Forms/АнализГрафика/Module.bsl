
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОтпускаСотрудников = ПолучитьИзВременногоХранилища(Параметры.АдресГрафика);
	
	ГрафикОтпусков.Обновление = Ложь;
	ГрафикОтпусков.Очистить();
	ГрафикОтпусков.ОтображениеТекстаЗначения = ОтображениеТекстаЗначенияДиаграммыГанта.Право;
	ГрафикОтпусков.ОтображениеТекстаИнтервала = ОтображениеТекстаИнтервалаДиаграммыГанта.Отображать;
	
	ШкалаВремени = ГрафикОтпусков.ОбластьПостроения.ШкалаВремени;
	ШкалаВремени.Элементы[0].Единица  = ТипЕдиницыШкалыВремени.Месяц;
	
	Для Каждого Стр Из ОтпускаСотрудников Цикл
		МассивСклонения = ПолучитьСклоненияСтрокиПоЧислу("День"
													    , Стр.КоличествоДней
													    , ""
													    , "ЧС=Количественное"
													    , "ПД=Винительный");
													    
	    Точка = ГрафикОтпусков.УстановитьТочку(Стр.Сотрудник);    	    
	    Серия = ГрафикОтпусков.УстановитьСерию("График");
	    
	    Значение = ГрафикОтпусков.ПолучитьЗначение(Точка, Серия);
	    Значение.Текст = МассивСклонения[0];
	                   
	    Интервал = Значение.Добавить();
	    Интервал.Начало = Стр.ДатаНачала;
	    Интервал.Конец = Стр.ДатаОкончания;
	КонецЦикла;
	
	ГрафикОтпусков.Обновление = Истина;

КонецПроцедуры

#КонецОбласти