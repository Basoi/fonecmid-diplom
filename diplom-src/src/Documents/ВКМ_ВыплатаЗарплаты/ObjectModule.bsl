#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)
	
	// регистр ВКМ_ВзаиморасчетыССотрудниками	
	Для Каждого ТекСтрокаВыплаты из Выплаты Цикл
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Сотрудник = ТекСтрокаВыплаты.Сотрудник;
		Движение.Сумма = ТекСтрокаВыплаты.Сумма;
	КонецЦикла;
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.БлокироватьДляИзменения = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос; 
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_Док
	|ИЗ
	|	Документ.ВКМ_ВыплатаЗарплаты.Выплаты КАК ВКМ_ВыплатаЗарплатыВыплаты
	|ГДЕ
	|	ВКМ_ВыплатаЗарплатыВыплаты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник.Представление КАК СотрудникПредставление,
	|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(
	|			&ГраницаКонтроля,
	|			(Сотрудник) В
	|				(ВЫБРАТЬ
	|					ВТ_Док.Сотрудник КАК Сотрудник
	|				ИЗ
	|					ВТ_Док КАК ВТ_Док)) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
	|ГДЕ
	|	ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток < 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ГраницаКонтроля", Новый Граница(МоментВремени(), ВидГраницы.Включая));  
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл	
			СтрокаСообщения = СтрШаблон("По сотруднику %1 Не достаточно средств для выплат. Не хватает %2 рублей."
								, Выборка.СотрудникПредставление
								, Выборка.СуммаОстаток);
		
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрокаСообщения; 
			Сообщение.Сообщить();   
		
			Отказ = Истина;
		КонецЦикла;
	КонецЕсли;
		
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!
	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры

#КонецОбласти

#КонецЕсли