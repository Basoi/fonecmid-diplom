
#Область ПрограммныйИнтерфейс

// Отправка сообщений телеграм боту
//
// Параметры:
//  Сообщение - Строка - Сообщение для отправки
//
// Возвращаемое значение:
//	HttpОтвет
Функция ОтправитьСообщение(Сообщение) Экспорт
	
	ТокенБота = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	ИДГруппы = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	
	Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , , Новый ЗащищенноеСоединениеOpenSSL());
	Данные = новый структура;
	Данные.Вставить("chat_id", ИДГруппы);
	Данные.Вставить("text", Сообщение);
	
	СтрокаЗапроса = ВКМ_ОбщегоНазначенияHTTP.СтрокаJSON(Данные);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", ВКМ_ОбщегоНазначенияHTTP.ТипКонтентаJSON());
	
	Запрос = Новый HTTPЗапрос("bot" + ТокенБота + "/sendMessage", Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаЗапроса);
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ОписаниеОшибки = ВКМ_ОбщегоНазначенияHTTP.СтрокаJSON(Ответ);
		ЗаписьЖурналаРегистрации("HTTPСервисы.Ошибка", УровеньЖурналаРегистрации.Ошибка,,, 
		ОбработкаОшибок.ПодробноеПредставлениеОшибки(ОписаниеОшибки));
	КонецЕсли;
	
	Возврат Ответ;
	
КонецФункции // ОбработатьВходящийЗапрос()

// Проверка наличия уведомлений для телеграм бота
Процедура ПроверкаУведомленийТелеграм() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграмБоту.Ссылка КАК Ссылка,
	|	ВКМ_УведомленияТелеграмБоту.ТекстСообщения КАК ТекстСообщения
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл		
		Ответ = ОтправитьСообщение(Выборка.ТекстСообщения);	
		
		Если Ответ.КодСостояния = 200 Тогда
			Уведомление = Выборка.Ссылка.ПолучитьОбъект();
			Уведомление.Удалить();
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

#КонецОбласти