#Область ПрограммныйИнтерфейс

//Формирование РТУ по договорам обслуживания
//
// Параметры:
// 	Параметр1 - Структура - Данные заполнения РТУ.
//  Параметр2 - Строка  - Адрес временного хранилища
//
// Возвращаемое значение:
// 	Строка - Возвращает адрес временного хранилища с данными заполнения для формы РТУ 
//
Функция СформироватьРТУ(Параметр1, Параметр2) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка) КАК Документ";
	
	Результат = Запрос.Выполнить();
	ДанныеТаблицы = Результат.Выгрузить();
	ДанныеТаблицы.Очистить();
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент
	|ПОМЕСТИТЬ ВТ_Договоры
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	|	И &Дата МЕЖДУ ДоговорыКонтрагентов.ВКМ_ПериодНачалаДействияДоговора И ДоговорыКонтрагентов.ВКМ_ПериодОкончанияДействияДоговора
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Договоры.Организация КАК Организация,
	|	ВТ_Договоры.Ссылка КАК Договор,
	|	ВТ_Договоры.Контрагент КАК Контрагент,
	|	&Ответственный КАК Ответственный,
	|	""Создано автоматически"" КАК Комментарий,
	|	РеализацияТоваровУслуг.Ссылка КАК Документ
	|ИЗ
	|	ВТ_Договоры КАК ВТ_Договоры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО ВТ_Договоры.Ссылка = РеализацияТоваровУслуг.Договор
	|		И ВТ_Договоры.Организация = РеализацияТоваровУслуг.Организация
	|		И КОНЕЦПЕРИОДА(РеализацияТоваровУслуг.Дата, Месяц) = &Дата
	|";
	
	Запрос.УстановитьПараметр("Дата", КонецМесяца(Параметр1.Дата));
	Запрос.УстановитьПараметр("Ответственный", Пользователи.ТекущийПользователь());
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НовСтрока = ДанныеТаблицы.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтрока, Выборка);
		
		Если НЕ ЗначениеЗаполнено(Выборка.Документ) Тогда
			ДокументОбъект = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			ДокументОбъект.Дата = КонецМесяца(Параметр1.Дата);
			ДокументОбъект.Заполнить(Выборка);
			ДокументОбъект.ВКМ_ВыполнитьАвтозаполнение();
			ДокументОбъект.ПроверитьЗаполнение();
			ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
			НовСтрока.Документ = ДокументОбъект.Ссылка;
		КонецЕсли;	 
	КонецЦикла;
	
	ДанныеЗаполнения = ПоместитьВоВременноеХранилище(ДанныеТаблицы, Параметр2);
	Возврат ДанныеЗаполнения;	
	
КонецФункции

#КонецОбласти