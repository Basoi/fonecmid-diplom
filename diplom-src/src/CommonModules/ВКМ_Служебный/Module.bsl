
#Область ПрограммныйИнтерфейс

//Обработчик форм "ПриСозданииНаСервере"
//
// Параметры:
//  Форма - Форма - Форма
Процедура ПриСозданииНаСервере(Форма)Экспорт
	
	ИмяОбработчика = "ПриСоздании";
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда        	
		Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			ДоговорКонтрагентаФормаЭлемента(Форма, ИмяОбработчика);
		ИначеЕсли ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			РеализацияТоваровФормаЭлемента(Форма, ИмяОбработчика);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

//Обработчик форм "ПриИзменении"
//
// Параметры:
//  Форма - Форма - Форма
Процедура ПриИзменении(Форма)Экспорт
	
	ИмяОбработчика = "ПриИзменении";
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "Объект") Тогда        	
		Если ТипЗнч(Форма.Объект.Ссылка) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
			ДоговорКонтрагентаФормаЭлемента(Форма, ИмяОбработчика);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//Обработчик формы "ПриСозданииНаСервере" для справочника "ДоговорыКонтрагентов"
Процедура ДоговорКонтрагентаФормаЭлемента(Форма, ИмяОбработчика)
	
	ВКМ_ПериодНачалаДействияДоговора = Форма.Элементы.Найти("ВКМ_ПериодНачалаДействияДоговора");
	ВКМ_ПериодОкончанияДействияДоговора = Форма.Элементы.Найти("ВКМ_ПериодОкончанияДействияДоговора");
	ВКМ_СуммаАбонентскойПлаты = Форма.Элементы.Найти("ВКМ_СуммаАбонентскойПлаты");
	ВКМ_СтоимостьЧасаРабот = Форма.Элементы.Найти("ВКМ_СтоимостьЧасаРабот");
	
	Если ВКМ_ПериодНачалаДействияДоговора <> Неопределено И ВКМ_ПериодОкончанияДействияДоговора <> Неопределено 
		И ВКМ_СуммаАбонентскойПлаты <> Неопределено И ВКМ_СтоимостьЧасаРабот <> Неопределено Тогда
		ВКМ_АбонентскоеОбслуживание = Форма.Объект.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание;
		
		ВКМ_ПериодНачалаДействияДоговора.Видимость = ВКМ_АбонентскоеОбслуживание;
		ВКМ_ПериодОкончанияДействияДоговора.Видимость = ВКМ_АбонентскоеОбслуживание;
		ВКМ_СуммаАбонентскойПлаты.Видимость = ВКМ_АбонентскоеОбслуживание;		
		ВКМ_СтоимостьЧасаРабот.Видимость = ВКМ_АбонентскоеОбслуживание;			
	КонецЕсли;

КонецПроцедуры

//Обработчик формы "ПриСозданииНаСервере" для документа "РеализацияТоваровУслуг"
Процедура РеализацияТоваровФормаЭлемента(Форма, ИмяОбработчика)
	
	НоваяКоманда = Форма.Команды.Добавить("ВКМ_Заполнить");
	НоваяКоманда.Действие = "ВКМ_ВыполнитьАвтозаполнениеКлиент";
	НоваяКоманда.Заголовок = "ВКМ Заполнить";
	
	НовыйЭлемент = Форма.Элементы.Добавить("ВКМ_Заполнить",
	                                 Тип("КнопкаФормы"),
									 Форма.КоманднаяПанель);
	НовыйЭлемент.ИмяКоманды = "ВКМ_Заполнить";
	
КонецПроцедуры

#КонецОбласти